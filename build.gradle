plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.panderior'
archivesBaseName = "minireal-annotation"
version = '1.1.6'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    implementation 'com.squareup:javapoet:1.11.1'
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    implementation 'com.google.code.gson:gson:2.9.1'
    compileOnly 'com.google.auto.service:auto-service:1.0-rc6'
    annotationProcessor 'com.google.auto.service:auto-service:1.0-rc6'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.1'
    implementation 'org.springframework.kafka:spring-kafka:2.8.0'
    implementation files('libs/mason.20.jar')

    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
}

jar {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

tasks.withType(JavaCompile){
    options.encoding = "UTF-8"
}

java {
    withJavadocJar()
    withSourcesJar()
}

publish {
    dependsOn build
}

publishing {
    publications{
        mavenJava(MavenPublication){
            groupId = 'io.github.panderior'
            artifactId = 'minireal-annotation'
            version = '1.1.6'
            from components.java

            pom {
                name = 'MiniReal-Annotation '
                description = 'MiniReal-Annotation is a library that houses java annotations and the process to generate the Simulator class for the MiniReal Webservice which is used for distributed Agent Based Modeling Simulation (ABMS).'
                url = 'https://github.com/balab-abms/minireal-annotation-processing'
                inceptionYear = '2024'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'duguma'
                        name = 'Duguma Yeshitla'
                        email = 'dugyeshihm@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git:github.com/balab-abms/minireal-annotation-processing.git'
                    developerConnection = 'scm:git:ssh://github.com/balab-abms/minireal-annotation-processing.git'
                    url = 'https://github.com/balab-abms/minireal-annotation-processing'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getProperty('ossrhUsername')
                password = System.getProperty('ossrhPassword')
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

test {
    useJUnitPlatform()
}